import org.jetbrains.kotlin.gradle.dsl.KotlinCompile

plugins {
    id "java-gradle-plugin"
    id 'com.starter.publishing'
    alias(libs.plugins.starter.library.kotlin)
    alias(libs.plugins.gradle.publishplugin)
    alias(libs.plugins.droidsonroids.jacocotestkit)
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
    testLogging {
        events "skipped", "failed"
    }
    doLast {
        Thread.sleep(2000) // https://github.com/gradle/gradle/issues/16603
    }
}

pluginBundle {
    website = 'https://github.com/mateuszkwiecinski/easylauncher-gradle-plugin/'
    vcsUrl = 'https://github.com/mateuszkwiecinski/easylauncher-gradle-plugin.git'
    description = 'Set of plugins that might be useful for Multi-Module Android projects.'
    tags = ['android', 'launcher', "flavor", "buildType"]
}

gradlePlugin {
    plugins {
        quality {
            id = 'com.starter.easylauncher'
            displayName = 'Automatically customizes Android laucher icon per each build variant'
            implementationClass = 'com.project.starter.easylauncher.plugin.EasyLauncherPlugin'
        }
    }
}

configurations {
    testRuntimeDependencies.extendsFrom compileOnly
}

tasks.withType(PluginUnderTestMetadata).configureEach {
    pluginClasspath.from(configurations.testRuntimeDependencies)
}


tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        apiVersion = "1.5"
        languageVersion = "1.5"
    }
}
tasks.withType(JavaCompile).configureEach {
    options.release.set(JavaVersion.VERSION_1_8.majorVersion.toInteger())
}
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(JavaVersion.VERSION_17.toString())
    }
}

dependencies {
    implementation("org.codehaus.groovy:groovy-xml:${GroovySystem.getVersion()}")
    runtimeOnly(libs.usefulness.webp)
    compileOnly(libs.agp.gradle)

    testImplementation gradleTestKit()
    testImplementation(libs.junit.jupiter)
    testImplementation(libs.assertj.core)
    testImplementation(libs.junit.jupiter.params)
    testImplementation(libs.apache.commons.lang3)
}
