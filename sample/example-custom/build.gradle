apply plugin: 'com.starter.application.android'
apply plugin: 'com.starter.easylauncher'

android {
    namespace "com.example.custom"
    defaultConfig {
        minSdkVersion 23
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug

            //Debuggable, will get a default ribbon in the launcher icon
        }
        beta {
            matchingFallbacks = ["debug"]
            signingConfig signingConfigs.debug

            //Debuggable, will get a default ribbon in the launcher icon
            debuggable true
        }
        canary {
            matchingFallbacks = ["debug"]
            signingConfig signingConfigs.debug

            //Non-debuggable, will not get any default ribbon
            debuggable false
        }
        release {
            matchingFallbacks = ["debug"]
            signingConfig signingConfigs.debug

            //Non-debuggable, will not get any default ribbon
        }
    }

    flavorDimensions = ["environment", "different"]
    productFlavors {
        local {
            dimension "environment"
        }
        qa {
            dimension "environment"
        }
        staging {
            dimension "environment"
        }
        production {
            dimension "environment"
        }

        aaa {
            dimension "different"
        }
        bbb {
            dimension "different"
        }
    }
}

dependencies {
    implementation project(":adaptive-support")
}

easylauncher {
    defaultFlavorNaming true // Use flavor name for default ribbon, instead of the type name

    productFlavors {
        local {}
        qa {
            // Add one more filter to all `qa` variants
            filters = redRibbonFilter()
        }
        staging {
            filters = [
                    customColorRibbonFilter("custom", "#6600CC", "#FFFFFF", "bottom", 0.17)
            ]
        }
        production {}

        aaa {
            filters = customRibbon(
                    label: "aaa",
                    ribbonColor: "#6600CC",
                    labelColor: "#FFFFFF",
                    position: "topRight",
                    drawingOptions: ["IgnoreTransparentPixels", "AddExtraPadding"],
            )
        }
        bbb {
            filters = customColorRibbonFilter("bbb", "#EEEEEE", "#FF00FF", "bottom")
        }
    }

    buildTypes {
        beta {
            // Add two more filters to all `beta` variants
            filters = [
                    customColorRibbonFilter("#0000FF"),
                    overlayFilter(file("launcherOverlay/beta.png"))
            ]
        }
        canary {
            // Remove ALL filters to `canary` variants
            enable false
        }
        release {}
    }

    variants {
        productionAaaDebug {
            // OVERRIDE all previous filters defined for `productionAADebug` variant
            filters = orangeRibbonFilter("custom")
        }
        localBbbCanary {
            filters = chromeLike(label: "api<26", ribbonColor: "#0B3954", labelColor: "#FF6663")
        }
        localAaaDebug {
            filters = [
                    customRibbon(label: "local"),
                    chromeLike(label: "aaa"),
                    overlayFilter(file("launcherOverlay/beta.png")),
            ]
        }
    }
}
